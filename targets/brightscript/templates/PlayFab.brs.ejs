function PlayFab() as object
	if m.PlayFab = invalid
		m.PlayFab = {
			Settings: {
				TitleID: invalid,
				AsyncEnabled: false,
				DebuggingEnabled: false,
				Timeout: 20000,
				DeveloperSecretKey: invalid
			}
			_internalSettings: {
				SdkVersionString: "<%- sdkVersion %>",
				PlayFabID: invalid,
				SessionTicket: invalid,
				EntityID: invalid,
				EntityType: invalid,
				EntityToken: invalid,
				GetServerURL: invalid,
				ErrorTitleId: "Must have PlayFab.Settings.TitleID set to call this method",
				ErrorLoggedIn: "Must be logged in to call this method",
				ErrorEntityToken: "You must successfully call GetEntityToken before calling this",
				ErrorSecretKey: "Must have PlayFab.Settings.DeveloperSecretKey set to call this method"
			}
			Post: invalid,
			Wait: invalid
		}

		m.PlayFab._internalSettings.GetServerURL = function(methodUrl as string) as string
			return "https://" + PlayFab().Settings.TitleID + ".playfabapi.com" + methodUrl
		end function

		m.PlayFab._internalSettings.CreateUrlTransfer = function() as object
			message_port = CreateObject("roMessagePort")
			transfer = CreateObject("roUrlTransfer")
			transfer.SetMessagePort(message_port)
			return transfer
		end function

		m.PlayFab.Post = function(url_path, request, auth_key = invalid, auth_val = invalid, extra_headers = invalid) as dynamic
			url = PlayFab()._internalSettings.GetServerURL(url_path)
			json = FormatJSON(request)

			if PlayFab().Settings.DebuggingEnabled
				print "::PlayFab:: Request - " + url_path + " " + json
			end if

			transfer = PlayFab()._internalSettings.CreateUrlTransfer()
			transfer.SetCertificatesFile("common:/certs/ca-bundle.crt")
			transfer.InitClientCertificates()
			transfer.SetUrl(url)

			if extra_headers <> invalid
				for each key in extra_headers
					transfer.AddHeader(key, extra_headers[key])
				end for
			end if
			transfer.AddHeader("Content-Type", "application/json")
			transfer.AddHeader("X-ReportErrorAsSuccess", "true")
			sdk_version = PlayFab()._internalSettings.SdkVersionString
			if sdk_version <> invalid
				transfer.AddHeader("X-PlayFabSDK", sdk_version)
			end if
			if auth_key <> invalid and auth_val <> invalid
				transfer.AddHeader(auth_key, auth_val)
			end if

			sent_successfully = transfer.AsyncPostFromString(json)
			post = {
				Transfer: transfer,
				Result: invalid,
				IsAsync: PlayFab().Settings.AsyncEnabled,
				SentSuccessfully: sent_successfully
			}

			if not post.IsAsync and post.SentSuccessfully
				PlayFab().Wait(post)
			end if

			return post
		end function

		m.PlayFab.Wait = function(post as object)
			message_port = post.Transfer.GetMessagePort()
			timer = CreateObject("roTimeSpan")
			timeout = PlayFab().Settings.Timeout
			waiting = true
			while waiting
				message = message_port.GetMessage()
				if message <> invalid
					post.Result = message
					waiting = false

					if PlayFab().Settings.DebuggingEnabled
						print "::PlayFab:: Result - " + post.Transfer.GetUrl().Replace("https://" + PlayFab().Settings.TitleID + ".playfabapi.com", "") + " " + post.Result
					end if
				end if
				if timer.TotalMilliseconds() > timeout
					waiting = false

					if PlayFab().Settings.DebuggingEnabled
						print "::PlayFab:: Timed Out - " + post.Transfer.GetUrl().Replace("https://" + PlayFab().Settings.TitleID + ".playfabapi.com", "")
					end if
				end if
			end while
		end function
	end if

	return m.PlayFab
end function
